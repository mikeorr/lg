#!/usr/bin/perl
# Created by Ben Okopnik on Mon Dec 22 23:13:11 EST 2003
use warnings;
use strict;

my ( $ans, %loc );

$loc{IP} = "10.0.0.120";
$loc{netmask} = "255.255.255.0";
$loc{resolver} = "10.0.0.2";
$loc{router} = "10.0.0.1";

for my $var ( sort keys %loc ){
	print "What is the $var? (default: $loc{$var}) ";
	chomp( $ans = <STDIN> );
	if ( $ans =~ /\d+\.\d+\.\d+\.\d+/ ){
		# Script, modify thyself!
		local ( $^I, $ARGV[0] ) = ( '', $0 );
		while ( <> ){
			s/^(\$loc{$var} = ").*(";)$/$1$ans$2/;
			print;
		}
	}
}

print <<`ShellOut`
ifconfig eth0 $loc{IP} netmask $loc{netmask}
echo "nameserver $loc{resolver}" > /etc/resolv.conf
route add default gw $loc{router}
ShellOut
