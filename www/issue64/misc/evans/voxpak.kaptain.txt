#!/home/paul/bin/kaptain -V 
# voxpak: a frontend for managing vox/fax messages from vgetty
# run with -geometry 600x540+236+105  in icon, don't know how to force this yet otherwise
# 2001-01-12 08:20 pevans@technologist.com
#


start "VoxPak" -> descr msglist faxlist;

descr :horizontal -> title onoff pic;
	title :horizontal -> @text("VGETTY MANAGER") @text(x%cat /etc/nologin.modem%);
	onoff :horizontal -> @execclose(on_sh)="On" @execclose(off_sh)="Off";
	pic -> @icon("/usr/share/icons/large/kvoice.xpm");

msglist :framed :horizontal -> msg vbuttons;
	msg  "Voice Messages" -> @list(`ls -Fr /var/spool/voice/incoming | grep -v /`)="";
	vbuttons -> play editmsg renmsg movmsg delmsg;
		play -> @action(play_msg_sh)=" Play Msg  ";
		editmsg -> @action(edit_msg_sh)=" Edit Msg  ";
		renmsg -> @button(ren_msg)="  Rename   ";
		movmsg -> @button(mov_msg)="    Move    ";
		delmsg -> @execclose(del_msg_sh)="Delete Msg";

faxlist :framed :horizontal -> fax fbuttons;
	fax "Fax Messages" -> @list(`ls /var/spool/fax/incoming`);
	fbuttons -> viewfax renfax movfax delfax close;
		viewfax -> @action(view_fax_sh)="  View Fax ";
		renfax -> @button(ren_fax)="  Rename   ";
		movfax -> @button(mov_fax)="    Move    ";
		delfax -> @execclose(del_fax_sh)=" Delete Fax";
		close :framed -> dismiss;
			dismiss -> @close="Exit";

# vox msg actions
#

play_msg_sh -> "rmdtopvf /var/spool/voice/incoming/"msg" | pvftowav | play -t wav - ";
edit_msg_sh -> "rmdtopvf /var/spool/voice/incoming/"msg" | pvftowav >/tmp/msg.wav;gnoise /tmp/msg.wav ";
ren_msg "Rename Message to.." -> new_mname ren_msg_buttons; 
	new_mname :beside "    New Name:" -> @string()=".rmd";
	ren_msg_buttons -> @execclose(ren_msg_sh)="Rename" @close="Cancel";
		ren_msg_sh -> "mv /var/spool/voice/incoming/"msg" /var/spool/voice/incoming/"new_mname;

mov_msg "Move Message to.." -> msg_dir mov_msg_buttons; 
	msg_dir -> @directory;
	mov_msg_buttons -> @execclose(mov_msg_sh)="Move" @close="Cancel";
		mov_msg_sh -> "mv /var/spool/voice/incoming/"msg " " msg_dir;

del_msg_sh -> "rm /var/spool/voice/incoming/"msg";voxpak -geometry 600x540+236+105";

on_sh -> "rm -f /etc/nologin.modem;voxpak -geometry 600x540+236+105";
off_sh -> "echo -n off > /etc/nologin.modem;voxpak -geometry 600x540+236+105";

# fax msg actions
#

view_fax_sh -> "kfax /var/spool/fax/incoming/"fax;

ren_fax "Rename Fax to.." -> new_fname ren_fax_buttons;
	new_fname :beside "    New Name:" -> @string(100)=".fax"; 
	ren_fax_buttons -> @execclose(rename_fax)="Save" @close="Cancel";
		rename_fax -> "mv /var/spool/voice/incoming/"fax" /var/spool/voice/incoming/"new_fname;

mov_fax "Move Fax to.." -> fax_dir mov_fax_buttons; 
	fax_dir -> @directory;
	mov_fax_buttons -> @execclose(mov_fax_sh)="Move" @close="Cancel";
		mov_fax_sh -> "mv /var/spool/fax/incoming/"fax " " fax_dir;

del_fax_sh -> "rm /var/spool/fax/incoming/"fax";voxpak -geometry 600x540+236+105";

